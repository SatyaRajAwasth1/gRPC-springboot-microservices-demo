buildscript {
    ext {
        protobufVersion = '3.23.4'
        protobufPluginVersion = '0.8.18'
        grpcVersion = '1.58.0'
    }
}

plugins {
    id 'java-library'
    id 'com.google.protobuf' version "${protobufPluginVersion}"
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    api "io.grpc:grpc-protobuf:${grpcVersion}"
    api "io.grpc:grpc-stub:${grpcVersion}"
    api "com.google.protobuf:protobuf-java:${protobufVersion}"
    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5' // Java 9+ compatibility - Do NOT update to 2.0.0
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/java/org/example/proto'
        }
        java {
            srcDirs 'src/main/java',
                    'build/generated/source/proto/main/java',
                    'build/generated/source/proto/main/grpc'
        }
    }
}

clean {
    delete 'build/generated'
}

// For publishing to local repository
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'com.example'
            artifactId = 'socialmedia-commons'
            version = '1.0.0'
        }
    }
    repositories {
        maven {
            name = 'localMavenRepo'
            url = uri("${project.rootDir}/../local-maven-repo")
        }
    }
}